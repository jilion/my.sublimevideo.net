# The SitesStatus handle the polling of the first not up-to-date site.
#
class MySublimeVideo.UI.SitesStatus
  constructor: (table) ->
    @table      = table
    @inProgress = {}
    this.setInProgess()
    this.delaySetCompletedStatus()

  setInProgess: ->
    @table.find("tr[id*='site']").each (i, element) =>
      el = $(element)
      if el.find("td.status .in_progress").exists()
        token = el.data('site-token')
        cdnUpdatedAt = parseInt(el.find("td.status").data('cdn-updated-at')) * 1000
        unless cdnUpdatedAt == 0
          @inProgress[token] = cdnUpdatedAt

  delaySetCompletedStatus: ->
    _.each @inProgress, (cdnUpdatedAt, token) =>
      now = new Date().getTime()
      upToDateIn = (300 * 1000) - (now - cdnUpdatedAt)
      _.delay(this.setCompletedStatus, upToDateIn, token)

  setCompletedStatus: (token) =>
    tr = @table.find("tr[data-site-token*='#{token}']")
    tr.find("td.status .in_progress").hide()
    tr.find("td.status .completed").show()
