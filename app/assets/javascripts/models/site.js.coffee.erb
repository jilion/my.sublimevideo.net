class MSV.Models.Site extends Backbone.Model
  defaults:
    token: null
    hostname: null
    plan_name: 0
    plan_video_views: 0
    plan_month_cycle_start_time: null
    plan_month_cycle_end_time: null
    stats_retention_days: null
    stats_trial_start_time: null
    trial_start_time: null

  title: ->
    if this.get('hostname')? && this.get('hostname') != '' then this.get('hostname') else "##{this.get('token')}"

  planMonthCycleStartTime: ->
    parseInt(this.get('plan_month_cycle_start_time')) * 1000

  planMonthCycleEndTime: ->
    parseInt(this.get('plan_month_cycle_end_time')) * 1000

  statsRetentionDays: ->
    this.get('stats_retention_days')

  isInFreePlan: ->
    this.get('stats_retention_days') == 0

  statsTrialStartTime: ->
    parseInt(this.get('stats_trial_start_time')) * 1000

  trialStartTime: ->
    parseInt(this.get('trial_start_time')) * 1000

  statsTrialEndTime: (statsTrialStartTime = this.statsTrialStartTime()) ->
    date = new Date(statsTrialStartTime)
    startTimeMidnight = Date.UTC(date.getUTCFullYear(), date.getUTCMonth(), date.getUTCDate())
    startTimeMidnight + 8 * 24 * 3600 * 1000

  statsTrialIsActivable: ->
    this.isInFreePlan() && this.statsTrialStartTime() == 0 && this.trialStartTime() == 0

  token: ->
    if MSVStats.statsRouter? && MSVStats.statsRouter.isDemo?
      'demo'
    else
      this.get('token')

class MSV.Collections.Sites extends Backbone.Collection

  model: MSV.Models.Site
  url: '/sites'

  select: (token) ->
    token = '<%= SiteToken.www %>' if token == 'demo'
    @selectedSite = _.find this.models, (site) =>
      site.get('token') == token
    this.trigger('change')

  selectedSiteIsInFreePlan: ->
    if @selectedSite?
      @selectedSite.isInFreePlan()

