
class MySublimeVideo.Helpers.StatsPoller
  constructor: (@stats) ->
    @subscribeToPusherChannel()
    @lastMinute = @currentMinute()
    @loopAndRefresh()

  subscribeToPusherChannel: ->
    channelName = @stats.data('pusher-channel')

    @pusher = new Pusher('<%= PusherWrapper.key %>', encrypted: true)
    channel = @pusher.subscribe(channelName)
    channel.bind 'play', (data) => @refreshTopStatsPage()

  loopAndRefresh: =>
    if @isNewMinute()
      @refreshTopStatsPage()
      @refreshBottomStatsPage() if @isNewHours()
    @timeout = setTimeout this.loopAndRefresh, 1000

  refreshTopStatsPage: ->
    if @isNewSecond()
      MySublimeVideo.videoStats.refreshTopStats()

  refreshBottomStatsPage: ->
    MySublimeVideo.videoStats.refreshBottomStats()

  isNewSecond: ->
    if @lastSecond isnt @currentSecond()
      @lastSecond = @currentSecond()
      true

  isNewMinute: ->
    if @lastMinute isnt @currentMinute()
      @lastMinute = @currentMinute()
      true

  isNewHours: -> @currentMinute() is 0

  currentMinute: -> new Date().getMinutes()
  currentSecond: -> new Date().getSeconds()

  teardown: ->
    clearTimeout(@timeout)
    @pusher.disconnect()




